*May 1, 2006
*Doc. ID: 79480, S-60674, Rev. B
*File Name: SUM110P06-07L_PS.txt and SUM110P06-07L_PS.lib
*This document is intended as a SPICE modeling guideline and does not
*constitute a commercial product data sheet.  Designers should refer to the
*appropriate data sheet of the same number for guaranteed specification
*limits.
.SUBCKT SUM110P06-07L 4 1 2
M1  3 1 2 2 PMOS W=21247402u L=0.25u           
M2  2 1 2 4 NMOS W=21247402u L=0.45u 
R1  4 3     RTEMP 15E-4
CGS 1 2     10000E-12
DBD 4 2     DBD
***************************************************************  
.MODEL  PMOS       PMOS  ( LEVEL  = 3          TOX    = 5E-8
+ RS     = 36E-4           RD     = 0          NSUB   = 1.3E17
+ KP     = 4.1E-6          UO     = 400             
+ VMAX   = 0               XJ     = 5E-7       KAPPA  = 1E-2
+ ETA    = 1E-4            TPG    = -1  
+ IS     = 0               LD     = 0                        
+ CGSO   = 0               CGDO   = 0          CGBO   = 0 
+ NFS    = 0.8E12          DELTA  = 0.1)
*************************************************************** 
.MODEL  NMOS       NMOS  ( LEVEL  = 3          TOX    = 5E-8
+NSUB    = 2.5E16            TPG    = -1)   
*************************************************************** 
.MODEL DBD D (CJO=1500E-12 VJ=0.38 M=0.32
+RS=0.1 FC=0.5 IS=1E-12 TT=6E-8 N=1 BV=60.2)
*************************************************************** 
.MODEL RTEMP RES (TC1=15E-3 TC2=5.5E-6)
*************************************************************** 
.ENDS
